plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 32

    def appConstantsFile = rootProject.file('configs/appConstants.properties')
    def appConstants = new Properties()
    appConstants.load(new FileInputStream(appConstantsFile))

    def appVersionsFile = rootProject.file('configs/appVersions.properties')
    def appVersions = new Properties()
    appVersions.load(new FileInputStream(appVersionsFile))

    defaultConfig {
        applicationId "com.joseph.myapp"
        minSdk 21
        targetSdk 32
        versionCode appVersions['VERSION_CODE'].toInteger()
        versionName appVersions['VERSION_NAME']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField 'String', 'BASE_AUTH_API_URL', appConstants['BASE_AUTH_API_URL']
        buildConfigField 'String', 'BASE_DATA_API_URL', appConstants['BASE_DATA_API_URL']
        buildConfigField 'String', 'REFRESH_TOKEN', appConstants['REFRESH_TOKEN']
        buildConfigField 'String', 'CLIENT_ID', appConstants['CLIENT_ID']
        buildConfigField 'String', 'CLIENT_SECRET', appConstants['CLIENT_SECRET']
        buildConfigField 'String', 'DATABASE_NAME', appConstants['DATABASE_NAME']
        buildConfigField 'String', 'DATABASE_TABLE_NAME', appConstants['DATABASE_TABLE_NAME']
        buildConfigField 'int', 'DATABASE_VERSION', appVersions['DATABASE_VERSION']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    /**
     * Compose
     */
    implementation 'androidx.compose.ui:ui:1.2.0-alpha07'
    implementation 'androidx.compose.material:material:1.2.0-alpha07'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.1.1'
    implementation 'androidx.navigation:navigation-compose:2.4.2'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.activity:activity-compose:1.4.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.1'
    debugImplementation 'androidx.compose.ui:ui-tooling:1.1.1'

    /**
     * Lottie
     */
    def lottie_version = '4.0.0'
    implementation "com.airbnb.android:lottie-compose:$lottie_version"

    /**
     * Dependency injection
     */
    def hilt_version = '2.40.1'
    def hilt_view_model_version = '1.0.0-alpha03'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_view_model_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_view_model_version"

    /**
     * Multidex
     */
    def multi_dex_version = '2.0.1'
    implementation "androidx.multidex:multidex:$multi_dex_version"

    /**
     * Logs
     */
    def timber_version = '5.0.1'
    implementation "com.jakewharton.timber:timber:$timber_version"

    /**
     * Crash handling
     */
    def fire_crasher_version = '2.0.0'
    implementation "com.github.osama-raddad:FireCrasher:$fire_crasher_version"

    /**
     * Network
     */
    def network_response_adapter_version = '4.2.2'
    def retrofit_version = '2.9.0'
    def stetho_version = '1.5.1'
    def logging_interceptor_version = '3.1.0'
    implementation "com.github.haroldadmin:NetworkResponseAdapter:$network_response_adapter_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"
    implementation("com.github.ihsanbal:LoggingInterceptor:$logging_interceptor_version") {
        exclude group: 'org.json', module: 'json'
    }

    /**
     * Coroutines
     */
    def coroutines_version = '1.5.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    /**
     * Secured preferences
     */
    def secured_prefs_version = '0.1.7'
    implementation "com.scottyab:secure-preferences-lib:$secured_prefs_version"

    /**
     * Database
     */
    def room_version = '2.4.2'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    /**
     * SwipeRefresh
     */
    def accompanist_swipe_refresh_version = '0.24.3-alpha'
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_swipe_refresh_version"
}